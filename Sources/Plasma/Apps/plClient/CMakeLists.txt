set(external_SCRIPTS
    external/create_resource_dat.py
    external/makeres.py
    external/render_svg.py
    external/scalergba.py
)

set(external_SOURCES
    external/Cursor_Base.svg
    external/Linking_Book.svg
    external/Loading_Text_rasterfont.svg
    external/Voice_Chat.svg
)
find_program(
    PNGCRUSH_EXECUTABLE pngcrush
    DOC "Path to pngcrush"
)

# Can we generate the resource.dat file?
python_test_modules(
    MODULES cairosvg PIL
    REQUIREMENTS_FILE "${CMAKE_CURRENT_SOURCE_DIR}/external/requirements.txt"
)

cmake_dependent_option(PLASMA_BUILD_RESOURCE_DAT "Do we want to build resource.dat?" ON "cairosvg_FOUND AND PIL_FOUND" OFF)
cmake_dependent_option(RESOURCE_OPTIMIZE "Optimize the images in resource.dat" ON "PLASMA_BUILD_RESOURCE_DAT AND PNGCRUSH_EXECUTABLE" OFF)
cmake_dependent_option(RESOURCE_BRUTE "Allow pngcrush brute-force optimization" OFF "PLASMA_BUILD_RESOURCE_DAT AND PNGCRUSH_EXECUTABLE" OFF)

if(PLASMA_BUILD_RESOURCE_DAT)
    set(external_DAT "${CMAKE_CURRENT_BINARY_DIR}/resource.dat")
    if(RESOURCE_OPTIMIZE)
        string(APPEND OPTIMIZE_ARGUMENT "--pngcrush \"${PNGCRUSH_EXECUTABLE}\" ")
    endif()
    if(RESOURCE_BRUTE)
        string(APPEND OPTIMIZE_ARGUMENT "--brute ")
    endif()

    add_custom_command(
        OUTPUT ${external_DAT}
        COMMAND "${Python3_EXECUTABLE}"
                "${CMAKE_CURRENT_SOURCE_DIR}/external/makeres.py"
                --render --package
                ${OPTIMIZE_ARGUMENT}
                -i "${CMAKE_CURRENT_SOURCE_DIR}/external"
                -w "${CMAKE_CURRENT_BINARY_DIR}"
                -o "${CMAKE_CURRENT_BINARY_DIR}"
        DEPENDS ${external_SCRIPTS} ${external_SOURCES}
    )

    install(
        FILES "${external_DAT}"
        DESTINATION client
    )
endif()

set(plClient_HEADERS
    plClient.h
    plClientCreatable.h
    plClientLoader.h
    plClientUpdateFormat.h
)

set(plClient_SOURCES
    pfAllCreatables.cpp
    plAllCreatables.cpp
    plClient.cpp
    plClientLoader.cpp
    pnAllCreatables.cpp
)

if(WIN32)
    list(APPEND plClient_SOURCES
        plClient_Win.cpp
        winmain.cpp
    )
elseif(APPLE)
    include_directories("../../PubUtilLib/plPipeline/GL/OSX")
    set(plClient_SOURCES ${plClient_SOURCES}
        OSX/main.mm
        OSX/PLSKeyboardEventMonitor.mm
        OSX/PLSView.mm
        OSX/PLSLoginWindowController.mm
    )
    set(plClient_HEADERS ${plClient_HEADERS}
        OSX/PLSKeyboardEventMonitor.h
        OSX/PLSView.h
        OSX/PLSLoginWindowController.h
    )
    set(RESOURCES 
        OSX/MainMenu.xib
        OSX/PLSLoginWindowController.xib
        OSX/banner@2x.jpeg
        res/Dirt.ICO
    )
    #shaders need to be compiled as part of the app
    #this could change in the future, but for now the Metal code expects the library to be compiled in the app
    set(plClient_SHADERS
	../../FeatureLib/pfMetalPipeline/ShaderSrc/FixedPipelineShaders.metal
	../../FeatureLib/pfMetalPipeline/ShaderSrc/PlateShaders.metal
	../../FeatureLib/pfMetalPipeline/ShaderSrc/BiasNormals.metal
	../../FeatureLib/pfMetalPipeline/ShaderSrc/CompCosines.metal
	../../FeatureLib/pfMetalPipeline/ShaderSrc/WaveSet7.metal
	../../FeatureLib/pfMetalPipeline/ShaderSrc/Grass.metal
	../../FeatureLib/pfMetalPipeline/ShaderSrc/WaveDecEnv.metal
    ../../FeatureLib/pfMetalPipeline/ShaderSrc/Avatar.metal
    ../../FeatureLib/pfMetalPipeline/ShaderSrc/WaveDec1Lay_7.metal
    ../../FeatureLib/pfMetalPipeline/ShaderSrc/WaveRip.metal
    )
    set_source_files_properties(${plClient_SHADERS} PROPERTIES LANGUAGE METAL)
    source_group("Metal Shaders" FILES ${plClient_SHADERS})
    set(plClient_SOURCES ${plClient_SOURCES} ${plClient_SHADERS})
    include_directories("../../FeatureLib/pfMetalPipeline/metal-cpp"
                        "../../FeatureLib/pfMetalPipeline/ShaderSrc")
else()
    list(APPEND plClient_SOURCES
        main.cpp
    )
endif()

set(plClient_TEXT
    ${Plasma_SOURCE_DIR}/Docs/ReleaseNotes/ReleaseNotes.txt
)

set(plClient_RESOURCES
    res/plClient.exe.manifest
    res/plClient.rc
    res/resource.h
    res/banner.bmp
    res/cnsl1.bin
    res/Dirt.ICO
    res/headspin.ico
)

if(APPLE)
    add_executable(plClient MACOSX_BUNDLE ${plClient_SOURCES} ${plClient_HEADERS} ${RESOURCES}) 
    set(MAIN_NIBFILE "MainMenu")           
    set_target_properties(plClient PROPERTIES
        BUNDLE True
        RESOURCE "${RESOURCES}"
        MACOSX_BUNDLE_INFO_PLIST "${CMAKE_CURRENT_SOURCE_DIR}/OSX/Info.plist.in"
        MACOSX_BUNDLE_GUI_IDENTIFIER org.openuru.plClient
        MACOSX_BUNDLE_BUNDLE_VERSION "0.1"
        MACOSX_BUNDLE_SHORT_VERSION_STRING "0.1"
    )
    set_property (TARGET plClient APPEND_STRING PROPERTY COMPILE_FLAGS "-fobjc-arc")
    target_link_libraries(plClient PRIVATE "-framework Cocoa")
    target_link_libraries(plClient PRIVATE "-framework QuartzCore")
else()
    plasma_executable(plClient WIN32 CLIENT INSTALL_PDB
        SOURCES
            ${plClient_SOURCES} ${plClient_HEADERS}
            ${plClient_TEXT} ${plClient_RESOURCES}
    )
endif()

if(PLASMA_BUILD_RESOURCE_DAT)
    target_sources(plClient PRIVATE ${external_SCRIPTS} ${external_SOURCES} ${external_DAT})
endif()

target_link_libraries(
    plClient
    PRIVATE
        CoreLib

        # For the "all creatables"
        pnNucleusInc
        plPubUtilInc
        pfFeatureInc

        # Everything else used in this target.
        pnDispatch
        pnFactory
        pnKeyedObject
        pnMessage
        pnSceneObject
        plAgeLoader
        plAnimation
        plAudio
        plAvatar
        plClientResMgr
        plDrawable
        plFile
        plGImage
        plGLight
        plInputCore
        plMessage
        plModifier
        plNetClient
        plNetCommon
        plNetGameLib
        plPhysX
        plPipeline
        plProgressMgr
        plResMgr
        plScene
        plSDL
        plStatGather
        plStatusLog
        plUnifiedTime
        pfAnimation
        pfAudio
        pfCharacter
        pfConsole
        pfConsoleCore
        $<$<PLATFORM_ID:Windows>:pfCrashHandler>
        pfGameGUIMgr
        pfJournalBook
        pfLocalizationMgr
        pfMoviePlayer
        pfPasswordStore
        pfPatcher
        pfPython
        $<$<BOOL:${PLASMA_PIPELINE_DX}>:pfDXPipeline>
        $<$<BOOL:${PLASMA_PIPELINE_GL}>:pfGLPipeline>
        $<$<BOOL:${PLASMA_PIPELINE_METAL}>:pfMetalPipeline>
        CURL::libcurl
)

if(PLASMA_EXTERNAL_RELEASE)
    set_target_properties(plClient PROPERTIES OUTPUT_NAME "UruExplorer")
endif(PLASMA_EXTERNAL_RELEASE)

if(PLASMA_BUILD_RESOURCE_DAT)
    source_group("Client Resource Scripts" FILES ${external_SCRIPTS})
    source_group("Client Resource Images" FILES ${external_SOURCES})
endif()
source_group("Source Files" FILES ${plClient_SOURCES})
source_group("Header Files" FILES ${plClient_HEADERS})
source_group("Text" FILES ${plClient_TEXT})
source_group("Windows Resource Files" FILES ${plClient_RESOURCES})
